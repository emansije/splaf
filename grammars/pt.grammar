// Adapted from <divergent>coder's toy language grammar
// http://divergentcoder.com/javascript/playing-with-peg-js/
// and from David Majda's javascript interpreter
// https://github.com/dmajda/pegjs/blob/master/examples/javascript.pegjs

start "início"
  = statement*

statement "declaração"
  = _ lb stmt:(attribution / if / while / print / prompt) _ lb { 
      return stmt; 
    }

attribution "atribuição"
  = id:identifiername _ "=" _ val:AdditiveExpression { 
      return { 
        type: "attribution", 
        name: id, 
        value: val 
      } 
    }

functionname "nome de função"
  = "diz"
  / "enquanto"
  / "escrever"
  / "ler"
  / "pede"
  / "raiz_quadrada"
  / "se"

reserved "reservado"
  = functionname
  / "começa"
  / "e"
  / "então"
  / "falso"
  / "não"
  / "ou"
  / "termina"
  / "verdadeiro"

identifiername "identificador"
  = !reserved
    first:[a-zA-Z_áàãâéêíóõôúçÁÀÃÂÉÊÍÓÕÔÚÇ]
    rest:[0-9a-zA-Z_áàãâéêíóõôúçÁÀÃÂÉÊÍÓÕÔÚÇ]* {
      return first + rest.join("")
    }

MultiplicativeExpression
  = head:UnitaryExpression
    tail:(_ MultiplicativeOperator _ UnitaryExpression)* {
      var result = head;
      for (var i = 0; i < tail.length; i++) {
        result = {
          type:     "binaryop",
          operator: tail[i][1],
          args:     [result, tail[i][3]]
        };
      }
      return result;
    }

MultiplicativeOperator
  = operator:("*" / "/" / "%") !"=" { return operator; }

AdditiveExpression
  = head:MultiplicativeExpression
    tail:(_ AdditiveOperator _ MultiplicativeExpression)* {
      var result = head;
      for (var i = 0; i < tail.length; i++) {
        result = {
          type:     "binaryop",
          operator: tail[i][1],
          args:     [result, tail[i][3]]
        };
      }
      return result;
    }

AdditiveOperator
  = [+-]

ComparisonExpression "expressão comparativa"
  = head:AdditiveExpression
    tail:(_ ComparisonOperator _ AdditiveExpression)* {
      var result = head;
      for (var i = 0; i < tail.length; i++) {
        result = {
          type:     "binaryop",
          operator: tail[i][1],
          args:     [result, tail[i][3]]
        };
      }
      return result;
    }

ComparisonOperator "operador de comparação"
  = ">="
  / "for maior ou igual a"
  / "é maior ou igual a"
  / ">"
  / "for maior que"
  / "é maior que"
  / "<="
  / "for menor ou igual a"
  / "é menor ou igual a"
  / "<"
  / "for menor que"
  / "é menor que"
  / "=="
  / "for igual a"
  / "é igual a"
  / "igual a"
  / "for"
  / "é"
  / "!="
  / "for diferente de"
  / "é diferente de"
  / "diferente de"
  / "não for"
  / "não é"

UnitaryExpression "expressão unitária"
  = op:(NotOperator / "-" / "raiz_quadrada") _ operand:(UnitaryExpression / primary) {
      return { 
        type: "unaryop", 
        operator: op, 
        arg: operand 
      }
    }
  / primary

NotOperator "operador de negação"
  = "não for verdade que"
  / "não é verdade que"
  / "não"

LogicalAndExpression "expressão lógica 'e'"
  = head:ComparisonExpression
    tail:(ws+ "e" ws+ ComparisonExpression)* {
      var result = head;
      for (var i = 0; i < tail.length; i++) {
        result = {
          type:     "binaryop",
          operator: tail[i][1],
          args:     [result, tail[i][3]]
        };
      }
      return result;
    }

LogicalOrExpression "expressão lógica 'ou'"
  = head:LogicalAndExpression
    tail:(_ "ou" _ LogicalAndExpression)* {
      var result = head;
      for (var i = 0; i < tail.length; i++) {
        result = {
          type:     "binaryop",
          operator: tail[i][1],
          args:     [result, tail[i][3]]
        };
      }
      return result;
    }

primary "expressão primária"
  = (identifier / literal / parenthesis)

identifier "identificador"
  = id:identifiername { 
      return {
        type: "variable", 
        name:id
      }
    }

literal "literal"
  = val:(real / integer / text / boolean) {
      return {
        type: "literal",
        value: val
      }
    }

integer "inteiro"
  = digits:[0-9]+ { 
      return parseInt(digits.join("")); 
    }

real "real"
  = digits:(integer "." integer) { 
      return parseFloat(digits.join("")); 
    }

text "texto"
  = '"' str:([^\"]*) '"' {
      return str.join("")
    }

boolean "booleano"
  = boolval:("verdadeiro" / "falso") {
      return boolval == "verdadeiro" ? true : false
    }

parenthesis "parêntesis"
  = "(" val:LogicalOrExpression ")" {
      return val
    }

block "bloco"
  = _ "começa" _ lb stmts:statement* _ lb "termina" {
      return {
        type: "block",
        code: stmts
      }
    }

if "se"
  = "se" ws+ cond:LogicalOrExpression ws+ "então" _ lb
    _ ifval:(statement / block) _ lb
    _ elseval:("senão" _ lb (statement / block))? _ lb {
      return {
        type: "if",
        condition: cond,
        value: ifval,
        elsevalue: elseval ? elseval[3] : null
      }
    }

while "enquanto"
  = "enquanto" ws+ cond:LogicalOrExpression _ lb
    _ whileval:(statement / block) _ lb {
      return {
        type: "while",
        condition: cond,
        value: whileval
      }
    }

prompt "pedido"
  = ("pede" / "ler") ws+ id:identifiername _ lb {
      return {
        type: "prompt",
        name: id
      }
    }

print "apresentação"
  = ("diz" / "escrever") ws+ args:((ws? ComparisonExpression)+)? _ lb {
      return {
        type: "print",
        arguments: args ? args.map(function(a) { return a[1] }) : null
      }
    }

ws "um espaço em branco"
  = [ \t]

lb "mudança de linha"
  = [\r\n]*

_ "espaço em branco"
  = ws*
